 SAMPDAT: PROC OPTIONS(MAIN) REORDER;
  DCL SYSPRINT FILE OUTPUT STREAM;
  DCL (OLDDAT,NEWDAT) FILE OUTPUT RECORD SEQUENTIAL;
  DCL RECPTR POINTER;
  DCL 1 TAXRECORD BASED(RECPTR) UNALIGNED,
        %INCLUDE TAXREC;;
  DCL RECBUF CHAR(1028);
  DCL IDX FIXED BIN(31);

  RECPTR = ADDR(RECBUF);
 
  PUT FILE(SYSPRINT) LIST('HELLO');

  DO IDX = 2 TO 14 BY 2;
    TAXRECORD = '';
    TAXID = IDX;
    BALANCE = 100000*SIN(7*IDX);
    PUT STRING(NOTES(IDX/2))
      EDIT('TEST NOTE: ', 53*IDX)
          (A, F(3));
    WRITE FILE(OLDDAT) FROM(RECBUF);
    SELECT1:
    IF IDX = 4 THEN
      BEGIN;
        /* NEW */
        WRITE FILE(NEWDAT) FROM(RECBUF);
        TAXID = 5;
        WRITE FILE(NEWDAT) FROM(RECBUF);
        GOTO ENDSELECT1;
      END;
    ELSE IF IDX = 6 THEN
      ; /* DELETED*/
    ELSE IF IDX = 8 THEN
      BEGIN;
        /* UPDATED */
        BALANCE = 100000*SIN(6*IDX);
        PUT STRING(NOTES(IDX/2))
          EDIT('-- THIS NOTE WAS CHANGED: ', IDX*112, ' --')
              (A, F(3), A);
        WRITE FILE(NEWDAT) FROM(RECBUF);
        GOTO ENDSELECT1;
      END;
    ELSE
      WRITE FILE(NEWDAT) FROM(RECBUF);
    ENDSELECT1:
  END;

  PUT FILE(SYSPRINT) SKIP LIST ('DONE');
 END SAMPDAT;
